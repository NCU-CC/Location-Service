import org.gradle.api.plugins.tomcat.tasks.TomcatRun
import org.gradle.api.plugins.tomcat.tasks.TomcatStop

description = "jersey server for ncu location service"

apply plugin: 'tomcat'
apply plugin: 'tomcat-base'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.gradle.api.plugins:gradle-tomcat-plugin:1.2.4'
    }
}

repositories {
    maven { url "http://www.hibernatespatial.org/repository" }
}

dependencies {

    tomcat "org.apache.tomcat.embed:tomcat-embed-core:7.0.11",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:7.0.11"
    tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:7.0.11") {
        exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
    }
	
	testCompile ( 'com.jayway.restassured:rest-assured:2.3.3' ){
        exclude module: 'groovy'
    }

    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'

    runtime 'mysql:mysql-connector-java:5.1.32'
    runtime 'com.h2database:h2:1.4.181'

    compile project(":location-data")
    compile 'org.hibernate:hibernate-core:4.3.6.Final'
    compile 'org.hibernate:hibernate-c3p0:4.3.6.Final'
    compile 'org.hibernate:hibernate-spatial:4.3'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet-core:2.12'
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.12'
    compile 'org.apache.lucene:lucene-queryparser:4.10.0'
    compile 'org.apache.lucene:lucene-analyzers-smartcn:4.10.0'

}

war{
    archiveName = 'location-server.war'
}

ext {
    tomcatStopPort = 8592
    tomcatStopKey  = 'stopKey'
}

task integrationTomcatRun( type: TomcatRun ) {
    ajpPort  = 5566
    httpPort = 8591
    daemon = true
    stopPort = tomcatStopPort
    stopKey  = tomcatStopKey
}

task integrationTomcatStop( type: TomcatStop ) {
    stopPort = tomcatStopPort
    stopKey  = tomcatStopKey
}

task integrationTest( type: Test ) {
    include '**/*IntegrationTest.*'
    dependsOn   integrationTomcatRun
    finalizedBy integrationTomcatStop
}

test {
    exclude '**/*IntegrationTest.*'
}